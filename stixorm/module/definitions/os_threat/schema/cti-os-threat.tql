#
# Copyright (C) 2022 OS-Threat
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
define
    #####################################
    # Generic Incident Extension Objects
    #####################################
    incident-extension sub stix-sub-object,
        owns extension-type;
    ##################################


    #############################
    # Anecdote SCO
    #############################
    anecdote sub stix-cyber-observable-object,
        owns anecdote-value,
        owns report-date,
        plays provided-by:ancdote,
        plays anecdote-sco-ext:anecdote-base;
    # anecdote SCO extension Spec
    anecdote-sco-extension sub incident-extension,
        plays anecdote-sco-ext:anecdote-spec;
    # anecdote SCO embedded relation
    anecdote-sco-ext sub extensions,
        relates anecdote-base as sco,
        relates anecdote-spec as extension;


    ##############################
    # Sequence Object for Tasks/Events
    ################################
    sequence sub stix-domain-object, abstract,
        owns step-type,
        owns sequence-type,
        plays sequenced-object:sequence,
        plays on-completion:source,
        plays on-success:source,
        plays on-failure:source,
        plays next-steps:source,
        plays on-completion:target,
        plays on-success:target,
        plays on-failure:target,
        plays next-steps:target,
        plays sequence-ext:sequence-base;
    # Sequence Type Objects
    start-step sub sequence;
    end-step sub sequence;
    single-step sub sequence;
    parallel-step sub sequence;
    #########################################
    # sequence extension object
    sequence-extension sub incident-extension,
        plays sequence-ext:sequence-spec;
    # sequence extension embedded relation
    sequence-ext sub extensions,
        relates sequence-base as sco,
        relates sequence-spec as extension;
    #########################################
    # sequence on-completion embedded relation
    on-completion sub embedded,
        relates source as owner,
        relates target as pointed-to;
    #########################################
    # sequence on-success embedded relation
    on-success sub embedded,
        relates source as owner,
        relates target as pointed-to;
    #########################################
    # sequence on-failure embedded relation
    on-failure sub embedded,
        relates source as owner,
        relates target as pointed-to;
    #########################################
    # sequence next-steps embedded relation
    next-steps sub embedded,
        relates source as owner,
        relates target as pointed-to;
    #########################################


    ##################################
    #  Event Object
    ###################################
    event sub stix-domain-object,
        owns name,
        owns description,
        owns status,
        owns end-time,
        owns end-time-fidelity,
        owns event-types,
        owns goal,
        owns start-time,
        owns start-time-fidelity,
        plays blocks:stops,
        plays causes:caused,
        plays detects:found,
        plays performed:outcome,
	    plays based-on:basis,
        plays located-at:locating, # SRO
	    plays led-to:initial, # SRO
	    plays led-to:subsequent, # SRO
	    plays impacts:issue, # SRO
        plays event-refs:the-event,
        plays sequenced-object:object,
        plays list-of-sightings:sighting-event,
        plays objects-changed:change-driver,
        plays event-ext:event-base;
    # event extension object
    event-extension sub incident-extension,
        plays event-ext:event-spec;
    # event extension embedded relation
    event-ext sub extensions,
        relates event-base as sco,
        relates event-spec as extension;


    #######################################
    #  Impact Object
    #####################################
    impact sub stix-domain-object,
        owns impact-category,
        owns criticality,
        owns description,
        owns end-time,
        owns end-time-fidelity,
        owns recoverability,
        owns start-time,
        owns start-time-fidelity,
        plays impact-counter:impact-driver,
        plays impacted-refs:the-impact,
        plays superseded:superseded-by,
        plays superseded:precedes,
        plays impact-ext:impact-base,
        plays availability-imp:impact-base,
        plays confidentiality-imp:impact-base,
        plays external-imp:impact-base,
        plays integrity-imp:impact-base,
        plays monetary-imp:impact-base,
        plays physical-imp:impact-base,
        plays traceability-imp:impact-base,
        plays impact-refs:the-impact;
    # base impact extension object
    impact-extension sub incident-extension,
        plays impact-ext:impact-spec;
    # base impact extension embedded relation
    impact-ext sub extensions,
        relates impact-base as sco,
        relates impact-spec as extension;
    #######################################
    # Impact Specific Extensions
    ###################################
    #########################################
    # availability-impact extension object
    availability sub incident-extension,
        owns impacted-availability,
        plays availability-imp:availability-impact;
    # availability-impact extension embedded relation
    availability-imp sub extensions,
        relates impact-base as sco,
        relates availability-impact as extension;
    #########################################
    # confidentiality-impact extension object
    confidentiality sub incident-extension,
        owns information-type,
        owns loss-type,
        owns record-count,
        owns record-size,
        plays confidentiality-imp:confidentiality-impact;
    # confidentiality-impact extension embedded relation
    confidentiality-imp sub extensions,
        relates impact-base as sco,
        relates confidentiality-impact as extension;
    #########################################
    # external-impact extension object
    external sub incident-extension,
        owns impact-type,
        plays external-imp:external-impact;
    # external-impact extension embedded relation
     external-imp sub extensions,
        relates impact-base as sco,
        relates external-impact as extension;
    #########################################
    # integrity-impact extension object
    integrity sub incident-extension,
        owns alteration,
        owns information-type,
        owns record-count,
        owns record-size,
        plays integrity-imp:integrity-impact;
    # integrity-impact extension embedded relation
    integrity-imp sub extensions,
        relates impact-base as sco,
        relates integrity-impact as extension;
    #########################################
    # monetary-impact extension object
    monetary sub incident-extension,
        owns variety,
        owns conversion-rate,
        owns conversion-time,
        owns currrency,
        owns currency-actual,
        owns max-amount,
        owns min-amount,
        plays monetary-imp:monetary-impact;
    # monetary-impact extension embedded relation
    monetary-imp sub extensions,
        relates impact-base as sco,
        relates monetary-impact as extension;
    #########################################
    # physical-impact extension object
    physical sub incident-extension,
        owns impact-type,
        owns asset-type,
        plays physical-imp:physical-impact;
    # physical-impact extension embedded relation
    physical-imp sub extensions,
        relates impact-base as sco,
        relates physical-impact as extension;
    #########################################
    # traceability-impact extension object
    traceability sub incident-extension,
        owns traceability-type,
        plays traceability-imp:traceability-impact;
    # traceability-impact extension embedded relation
    traceability-imp sub extensions,
        relates impact-base as sco,
        relates traceability-impact as extension;
    #########################################



    ####################################
    #  Task Object
    ###################################
    task sub stix-domain-object,
        owns outcome,
        owns task-types,
        owns description,
        owns name,
        owns end-time,
        owns end-time-fidelity,
        owns error,
        owns int-priority,
        owns start-time,
        owns start-time-fidelity,
        plays task-ext:task-base,
        plays blocks:effort,
        plays causes:effort,
        plays creates:effort,
        plays uses:used,
        plays detects:effort,
        plays impacts:issue,
        plays located-at:locating,
        plays errored-to:started,
        plays errored-to:on-error,
        plays followed-by:initial,
        plays followed-by:subsequent,
        plays sequenced-object:object,
        plays assigned:effort,
        plays contact-for:issue,
        plays participated-in:assisted,
        plays owned-by:owned,
        plays impact-counter:impact-driver,
        plays performed:outcome,
        plays task-refs:the-task;
    # task extension object
    task-extension sub incident-extension,
        plays task-ext:task-spec;
    # task extension embedded relation
    task-ext sub extensions,
        relates task-base as sco,
        relates task-spec as extension;



    ############################################
    # Incident Core Extension Object
    #######################################
    # incident core extension
    incident-ext sub incident-extension,
        owns determination,
        owns investigation-status,
        owns malicious,
        owns blocked,
        owns criticality,
        owns recoverability,
        owns incident-types,
        plays impact-counter:impact-driver,
        plays scores:scored,
        plays task-refs:incident,
        plays event-refs:incident,
        plays impact-refs:incident,
	    plays occurence-ext:incident-core,
	    plays other-obj-refs:object;
    # incident core extension embedded relation
    occurence-ext sub extensions,
        relates occurence as sco,
        relates incident-core as extension;
    ############################################
    # Incident Core Sub Objects
    #######################################
    # Incident State Change Sub Object
    state-change sub stix-sub-object,
        owns state-change-type,
        plays  changed-objects-initial:changed-objects,
        plays  changed-objects-result:changed-objects,
        plays objects-changed:object-list;
    # Incident Score Sub Object
    incident-score sub stix-sub-object,
        owns name,
        owns description,
        owns score-value,
        plays scores:score;
    # key-value store for Entity-Counter object
    entity-value sub stix-attribute-string;
    entity-key sub stix-attribute-string,
        owns entity-value,
        plays impact-counter:counters;
    # Entity Counter embedded relation
    impact-counter sub embedded,
        relates impact-driver as owner,
        relates counters as pointed-to;


    ######################################
    # Identity contact extension
    ###################################
    identity-contact sub incident-extension,
        owns first-name,
        owns last-name,
        owns middle-name,
        owns prefix,
        owns suffix,
        owns team,
        plays identity-email:identity,
        plays identity-account:identity,
        plays identity-number:identity,
        plays identity-ext:identity-spec;
    # Identity contact extension embedded relation
    identity-ext sub extensions,
        relates identity-base as sco,
        relates identity-spec as extension;
    ######################################
    # Identity Sub Objects
    #####################################
    # Identity Contact Number Sub Object
    contact-number-sub sub stix-sub-object,
        owns description,
        owns contact-number,
        owns contact-number-type,
        plays identity-number:number-object;
    # Identity number sub-object embedded relation
    identity-number sub embedded,
        relates identity as owner,
        relates number-object as pointed-to;
    # Identity Email Contact Sub Object
    email-contact-sub sub stix-sub-object,
        owns description,
        owns digital-contact-type,
        plays email-ref:origin,
        plays identity-email:email-object;
    # Identity email sub-object embedded relation
    identity-email sub embedded,
        relates identity as owner,
        relates email-object as pointed-to;
    # Identity account email_ref embedded relation
    email-ref sub embedded,
        relates origin as owner,
        relates email as pointed-to;
    # Identity Social Media Contact Sub Object
    social-media-sub sub stix-sub-object,
        owns description,
        owns digital-contact-type,
        plays user-account-ref:origin,
        plays identity-account:account-object;
    # Identity account sub-object embedded relation
    identity-account sub embedded,
        relates identity as owner,
        relates account-object as pointed-to;
    # Identity account user_account_ref embedded relation
    user-account-ref sub embedded,
        relates origin as owner,
        relates account as pointed-to;



    ########################################
    # Evidence Extensions for Sighting object
    #####################################
    # base evidence extension objects
    sighting-extension sub stix-sub-object;
    ######################################
    # registered evidence extension spec
    evidence-extension sub sighting-extension,
        owns extension-type,
        plays evidence-ext:evidence-spec;
    # incident core extension embedded relation
    evidence-ext sub extensions,
        relates evidence-base as sco,
        relates evidence-spec as extension;
    #################################
    # evidence extension - Alerts
    alert-evidence sub sighting-extension,
        owns name,
        owns log,
        owns system-id,
        owns source,
        owns product,
        owns format,
        plays alert-ext:alert-extension;
    # alert evidence embedded relation
    alert-ext sub extensions,
        relates sighting-base as sco,
        relates alert-extension as extension;
    ##########################################
    # evidence extension - Context
    anecdote-evidence sub sighting-extension,
        owns person-name,
        owns person-context,
        owns report-submission,
        plays anecdote-ext:anecdote-extension;
    # alert evidence embedded relation
    anecdote-ext sub extensions,
        relates sighting-base as sco,
        relates anecdote-extension as extension;
    ##########################################
    # evidence extension - Context
    context-evidence sub sighting-extension,
        owns name,
        owns description,
        owns context-value,
        plays context-ext:context-extension;
    # alert evidence embedded relation
    context-ext sub extensions,
        relates sighting-base as sco,
        relates context-extension as extension;
    ##########################################
    # evidence extension - Exclusion List
    exclusion-evidence sub sighting-extension,
        owns source,
        owns channel,
        plays exclusion-ext:exclusion-extension;
    # alert evidence embedded relation
    exclusion-ext sub extensions,
        relates sighting-base as sco,
        relates exclusion-extension as extension;
    ##########################################
    # evidence extension - Enrichments
    enrichment-evidence sub sighting-extension,
        owns name,
        owns link-url,
        owns paid,
        owns enrichment-value,
        plays enrichment-ext:enrichment-extension;
    # alert evidence embedded relation
    enrichment-ext sub extensions,
        relates sighting-base as sco,
        relates enrichment-extension as extension;
    ##########################################
    # evidence extension - Hunts
    hunt-evidence sub sighting-extension,
        owns name,
        owns playbook-id,
        owns hunt-rule,
        plays hunt-ext:hunt-extension;
    # alert evidence embedded relation
    hunt-ext sub extensions,
        relates sighting-base as sco,
        relates hunt-extension as extension;
    ##########################################
    # evidence extension - Framework
    framework-evidence sub sighting-extension,
        owns framework,
        owns version,
        owns domain,
        owns comparison,
        owns comparison-approach,
        plays framework-ext:framework-extension;
    # alert evidence embedded relation
    framework-ext sub extensions,
        relates sighting-base as sco,
        relates framework-extension as extension;
    ##########################################
    # evidence extension - External Evidence
    external-evidence sub sighting-extension,
        owns source,
        owns version,
        owns last-update,
        owns pattern,
        owns pattern-type,
        owns payload,
        owns valid-from,
        owns valid-until,
        plays external-ext:external-extension;
    # alert evidence embedded relation
    external-ext sub extensions,
        relates sighting-base as sco,
        relates external-extension as extension;


    #####################################
    # Feed Objects and Relations
    ###################################
    ################################
    # Individual Feed Object
    feed sub stix-domain-object,
        owns name,
        owns description,
        owns paid,
        owns free,
        plays list-of-feeds:single-feed,
        plays feed-content:feed-owner;
    #######################################
    # Threat Sub Object Reference, sub object definition includes created and modified
    threat-sub-object sub stix-sub-object,
        plays obj-refs:object,
        plays obj-ref:container,
        plays feed-content:content;
    # Threat sub object embedded relation
    feed-content sub embedded,
		relates feed-owner as owner,
		relates content as pointed-to;
    ################################
    # Feeds Collection Object
    feeds sub stix-domain-object,
        owns name,
        owns description,
        owns paid,
        owns free,
        plays list-of-feeds:feed-list-owner;
    ########################################
    # Feed Embedded Relations
    list-of-feeds sub embedded,
		relates feed-list-owner as owner,
		relates single-feed as pointed-to;


    ##############################
    # Incident SRO's
    ########################
    led-to sub stix-core-relationship,
		relates initial as source,
		relates subsequent as target;

    impacts sub stix-core-relationship,
		relates issue as source,
		relates impacted as target;

    contact-for sub stix-core-relationship,
		relates contact as source,
		relates issue as target;

    detected sub stix-core-relationship,
		relates method as source,
		relates issue as target;

	performed sub stix-core-relationship,
	    relates initiator as source,
	    relates outcome as target;

    blocks sub stix-core-relationship,
		relates effort as source,
		relates stops as target;

    causes sub stix-core-relationship,
		relates effort as source,
		relates caused as target;

    detects sub stix-core-relationship,
		relates effort as source,
		relates found as target;

    creates sub stix-core-relationship,
		relates effort as source,
		relates made as target;

    errored-to sub stix-core-relationship,
		relates started as source,
		relates on-error as target;

    followed-by sub stix-core-relationship,
		relates initial as source,
		relates subsequent as target;

    assigned sub stix-core-relationship,
		relates owner as source,
		relates effort as target;

    participated-in sub stix-core-relationship,
		relates contact as source,
		relates assisted as target;

    derived-from sub stix-core-relationship,
		relates result as source,
		relates raw-material as target;

    #####################################
    # Incident Embedded Relations
    ####################################
    other-obj-refs sub embedded,
		relates referred as pointed-to,
		relates object as owner;

    list-of-sightings sub embedded,
        relates sighting-event as owner,
        relates sights as pointed-to;

    sequenced-object sub embedded,
        relates sequence as owner,
        relates object as pointed-to;

    objects-changed sub embedded,
        relates change-driver as owner,
        relates object-list as pointed-to;

    changed-objects-initial sub embedded,
        relates changed-objects as owner,
        relates changed as pointed-to;

    changed-objects-result sub embedded,
        relates changed-objects as owner,
        relates changed as pointed-to;

    impacted-refs sub embedded,
        relates the-impact as owner,
        relates impacted-thing as pointed-to;

    superseded sub embedded,
        relates superseded-by as pointed-to,
        relates precedes as owner;

    owned-by sub embedded,
        relates owned as owner,
        relates who as pointed-to;

    scores sub embedded,
        relates scored as owner,
        relates score as pointed-to;

    task-refs sub embedded,
        relates incident as owner,
        relates the-task as pointed-to;

    event-refs sub embedded,
        relates incident as owner,
        relates the-event as pointed-to;

    impact-refs sub embedded,
        relates incident as owner,
        relates the-impact as pointed-to;

    provided-by sub embedded,
        relates ancdote as owner,
        relates reporter as pointed-to;


    ##############################################################
	# Roles for Existing Objects to Connect to Incident Objects
	###############################################
	stix-domain-object
	    plays feed-content:content,
	    plays obj-ref:content,
	    plays changed-objects-initial:changed,
	    plays changed-objects-result:changed,
	    plays impacted-refs:impacted-thing,
	    plays other-obj-refs:referred;

	stix-cyber-observable-object
	    plays feed-content:content,
	    plays obj-ref:content,
	    plays impacted-refs:impacted-thing,
	    plays impacts:impacted,
	    plays other-obj-refs:referred;

	stix-core-relationship
	    plays feed-content:content,
	    plays other-obj-refs:referred;

    sighting
        plays list-of-sightings:sights,
        plays evidence-ext:evidence-base,
        plays alert-ext:sighting-base,
        plays anecdote-ext:sighting-base,
        plays context-ext:sighting-base,
        plays alert-ext:sighting-base,
        plays exclusion-ext:sighting-base,
        plays enrichment-ext:sighting-base,
        plays hunt-ext:sighting-base,
        plays framework-ext:sighting-base,
        plays external-ext:sighting-base;

    marking-definition
	    plays feed-content:content;

	identity
	    plays owned-by:who,
	    plays impacts:impacted,
	    plays contact-for:contact,
        plays assigned:owner,
        plays participated-in:contact,
        plays provided-by:reporter,
        plays identity-ext:identity-base;

	infrastructure
	    plays impacts:impacted;

	incident
	    plays occurence-ext:occurence,
	    plays led-to:initial,
	    plays led-to:subsequent,
	    plays impacts:issue,
	    plays contact-for:issue,
	    plays detected:issue,
        plays derived-from:raw-material;

    email-addr
        plays email-ref:email;

    user-account
        plays user-account-ref:account;

    indicator
        plays detected:method,
        plays creates:made;

    malware
        plays performed:initiator;

    tool
        plays performed:initiator;

    report
        plays derived-from:result;


    ##############################################
    # Attribute Definitions
    ############################################
    paid sub stix-attribute-boolean;
    free sub stix-attribute-boolean;
    malicious sub stix-attribute-boolean;
    blocked sub stix-attribute-boolean;

    status sub stix-attribute-string;
    end-time-fidelity sub stix-attribute-string;
    start-time-fidelity sub stix-attribute-string;
    event-types sub stix-attribute-string;
    goal sub stix-attribute-string;
    detection-methods sub stix-attribute-string;
    detection-system sub stix-attribute-string;
    detection-rule sub stix-attribute-string;
    state-change-type sub stix-attribute-string;
    impact-category sub stix-attribute-string;
    recoverability sub stix-attribute-string;
    information-type sub stix-attribute-string;
    loss-type sub stix-attribute-string;
    impact-type sub stix-attribute-string;
    alteration sub stix-attribute-string;
    variety sub stix-attribute-string;
    currrency sub stix-attribute-string;
    currency-actual sub stix-attribute-string;
    asset-type sub stix-attribute-string;
    traceability-type sub stix-attribute-string;
    evidence-type sub stix-attribute-string;
    source sub stix-attribute-string;
    outcome sub stix-attribute-string;
    task-types sub stix-attribute-string;
    step-type sub stix-attribute-string;
    error sub stix-attribute-string;
    determination sub stix-attribute-string;
    extension-type sub stix-attribute-string;
    investigation-status sub stix-attribute-string;
    incident-types sub stix-attribute-string;
    log sub stix-attribute-string;
    system-id sub stix-attribute-string;
    format sub stix-attribute-string;
    context-value sub stix-attribute-string;
    channel sub stix-attribute-string;
    link-url sub stix-attribute-string;
    enrichment-value sub stix-attribute-string;
    playbook-id sub stix-attribute-string;
    hunt-rule sub stix-attribute-string;
    framework sub stix-attribute-string;
    domain sub stix-attribute-string;
    comparison sub stix-attribute-string;
    comparison-approach sub stix-attribute-string;
    payload sub stix-attribute-string;
    person-name sub stix-attribute-string;
    person-context sub stix-attribute-string;
    report-submission sub stix-attribute-string;
    sequence-type sub stix-attribute-string;
    anecdote-value sub stix-attribute-string;
    contact-number sub stix-attribute-string;
    contact-number-type sub stix-attribute-string;
    digital-contact-type sub stix-attribute-string;
    first-name sub stix-attribute-string;
    last-name sub stix-attribute-string;
    middle-name sub stix-attribute-string;
    prefix sub stix-attribute-string;
    suffix sub stix-attribute-string;
    team sub stix-attribute-string;

    event-seq sub stix-attribute-integer;
    criticality sub stix-attribute-integer;
    record-count sub stix-attribute-integer;
    record-size sub stix-attribute-integer;
    impacted-availability sub stix-attribute-integer;
    score-value sub stix-attribute-integer;
    int-priority sub stix-attribute-integer;
    computers-mobile-count sub stix-attribute-integer;
    computers-personal-count sub stix-attribute-integer;
    computers-server-count sub stix-attribute-integer;
    customer-count sub stix-attribute-integer;
    customer-individual-count sub stix-attribute-integer;
    customer-organization-count sub stix-attribute-integer;
    domain-controller-count sub stix-attribute-integer;
    employee-count sub stix-attribute-integer;
    group-count sub stix-attribute-integer;
    ics-actuator-count sub stix-attribute-integer;
    ics-engineering-workstation-count sub stix-attribute-integer;
    ics-historian-count sub stix-attribute-integer;
    ics-hmi-count sub stix-attribute-integer;
    ics-other-count sub stix-attribute-integer;
    ics-plc-count sub stix-attribute-integer;
    ics-safety-system-count sub stix-attribute-integer;
    ics-sensor-count sub stix-attribute-integer;
    individual-count sub stix-attribute-integer;
    network-device-count sub stix-attribute-integer;
    organization-count sub stix-attribute-integer;
    system-count sub stix-attribute-integer;
    vehicles-count sub stix-attribute-integer;


    conversion-rate sub stix-attribute-double;
    max-amount sub stix-attribute-double;
    min-amount sub stix-attribute-double;

    end-time sub stix-attribute-timestamp;
    conversion-time sub stix-attribute-timestamp;
    last-update sub stix-attribute-timestamp;
    report-date sub stix-attribute-timestamp;


