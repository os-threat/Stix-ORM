#
# Copyright (C) 2025 OS-Threat
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
define
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # Generic MBC Extension Object/Relation
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    mbc-ext sub embedded,
        relates mbc as owner,
        relates extension as pointed-to;
    mbc-extension sub incident-extension,
	    plays mbc-ext:extension;
    
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # obj-defn Subobject
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    obj-defn sub embedded,
        relates object as owner,
        relates definition as pointed-to;
    
    external-reference
        plays obj-defn:definition;
    
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # References Subobject
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    references sub embedded,
        relates snippet as owner,
        relates reference as pointed-to;
    
    external-reference
        plays references:reference;

    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # Detection Rule Subobject
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    detect-ref sub embedded,
        relates rule as owner,
        relates method as pointed-to;

    detection-rules sub embedded,
        relates behavior as owner,
        relates rule as pointed-to;

    detection-rule sub stix-sub-object,
        owns rule-type,
        owns rule-name,
        owns rule,
        owns description,
        owns api-fncs,
        owns url-string,
        owns class-name,
        plays detect-ref:rule,
        plays detection-rules:rule;

    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # Snippet Object
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    author-ref sub embedded,
        relates snippet as owner,
        relates author as pointed-to;

    identity
        plays author-ref:author;

    exemplify-ref sub embedded,
        relates example as owner,
        relates method as pointed-to;

    snippets sub embedded,
        relates behavior as owner,
        relates code as pointed-to;

    snippet sub stix-sub-object,
        owns actual-snippet,
        owns language,
        owns description,
        owns actual-hash,
        plays references:snippet,
        plays author-ref:snippet,
        plays exemplify-ref:example,
        plays snippets:code;

    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # Tags Dictionary Definition
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    tags sub embedded,
        relates behavior as owner,
        relates tag as pointed-to;

    tag-value sub stix-attribute-string;
    tag-key sub stix-sub-object,
        owns dict-key,
        owns tag-value,
        plays tags:tag;

    ##############################
    # Malware Behavior Object
    ################################
    # contributor refs Foreign Key Relation
    contributor-refs sub embedded,
        relates object as owner,
        relates contributor as pointed-to;

    identity
        plays contributor-refs:contributor;

    # objective refs Foreign Key Relation
    objective-refs sub embedded,
        relates behavior as owner,
        relates objective as pointed-to;

    # related object refs Foreign Key Relation
    related-object-refs sub embedded,
        relates behavior as owner,
        relates related-object as pointed-to;

    technique
        plays related-object-refs:related-object;

    # malware behavior SDO
    malware-behavior sub stix-domain-object,
        owns name,
        owns micro,
        owns obj-version,
        plays obj-defn:object,
        plays related-object-refs:behavior,
        plays tags:behavior,
        plays objective-refs:behavior,
        plays snippets:behavior,
        plays detection-rules:behavior,
        plays contributor-refs:object,
        plays behavior-ref:behavior,
        plays uses:used,
        plays led-to:initial,
        plays led-to:subsequent,
        plays delivers:delivering,
	    plays mbc-ext:mbc;


    ##############################
    # Malware Method Object
    ################################
    # behavior Ref Foreign Key Relation
	behavior-ref sub embedded,
		relates method as owner,
		relates behavior as pointed-to;

	# Malware Method SDO
    malware-method sub stix-domain-object,
        owns name,
        owns micro,
        plays obj-defn:object,
        plays behavior-ref:method,
        plays contributor-refs:object,
        plays exemplify-ref:method,
        plays detect-ref:method,
        plays uses:used,
        plays led-to:initial,
        plays led-to:subsequent,
	    plays mbc-ext:mbc;

    ##############################
    # Malware Objective Object
    ################################
    malware-objective sub stix-domain-object,
        owns name,
        owns micro,
        plays obj-defn:object,
        plays uses:used,
        plays led-to:initial,
        plays led-to:subsequent,
        plays objective-refs:objective,
	    plays mbc-ext:mbc;


    ##############################
    # Malware Extension Object
    ################################
    malware-extension sub incident-extension,
        owns year,
        owns malware-platforms,
        plays obj-defn:object,
	    plays malware-ext:extension;
        
    malware-ext sub embedded,
        relates malware as owner,
        relates extension as pointed-to;

    malware
        plays malware-ext:malware;


    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # MBC Malware Attributes
    #^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
    # String
	    year sub  stix-attribute-string;
	    malware-platforms sub  stix-attribute-string;
	    obj-version sub  stix-attribute-string;
	    rule-type sub  stix-attribute-string;
	    rule-name sub  stix-attribute-string;
	    rule sub  stix-attribute-string;
	    api-fncs sub  stix-attribute-string;
	    actual-snippet sub  stix-attribute-string;
        language sub stix-attribute-string;
        actual-hash sub stix-attribute-string;
        url-string sub stix-attribute-string;
        dict-key sub stix-attribute-string;
        class-name sub stix-attribute-string;
    # Boolean
        micro sub stix-attribute-boolean;